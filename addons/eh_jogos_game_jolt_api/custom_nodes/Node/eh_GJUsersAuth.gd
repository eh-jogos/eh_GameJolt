# Node to manage requests to the `users/auth` endpoint. It wil instantiate and remove
# [eh_GJUsersAuthRequest] nodes as needed.
#
# It is able to read credentials from gamejolt's ".gj-credentials" the game launcher creates, 
# has methods to facilitate login, and auto-saving user credentials.
# @category: User
class_name eh_GJUsersAuth
extends Node

### Member Variables and Dependencies -------------------------------------------------------------
#--- signals --------------------------------------------------------------------------------------

# Signal emitted when auth request is successfuly completed. Sends an [eh_GJUsersAuthData] resource
# as parameter
signal gj_auth_completed(eh_gj_users_auth_data)
# Signal emitted when auth request fails. The dictionary format can vary according to the type of
# error
signal gj_auth_failed(error_dictionary)

# enums
#--- constants ------------------------------------------------------------------------------------

# Path where user credentials are saved as a binary file custom resource.
const USER_CREDENTIALS_PATH: String = "res://addons/eh_jogos_game_jolt_api/gj_user_credentials.res"

#--- public variables - order: export > normal var > onready --------------------------------------

# Flag to tell if player credentials has been successfully checked and logged in.
var is_logged_in: = false

#--- private variables - order: export > normal var > onready -------------------------------------

var _username: String = ""
var _user_token: String = ""

### ---------------------------------------


### Built in Engine Methods ---------------

func _ready():
	_auto_set_user_credentials()

### ---------------------------------------


### Public Methods ------------------------

# Returns player username retrieved either from saved credentials, or from ".gj-credentials" file
# generated by gamejolt launcher.
func get_player_username() -> String:
	return _username


# Returns player game token retrieved either from saved credentials, or from ".gj-credentials" file
# generated by gamejolt launcher.
func get_player_user_token() -> String:
	return _user_token


# Returns true if player credentials has already been found and/or saved.
func has_player_credentials() -> bool:
	var has_credentials: = false
	if _username != "" and _user_token != "":
		has_credentials = true
	
	return has_credentials


# Auto Auth request for the player. It's a helper for login in the current player without needing 
# to whory about username and game token, but it accepts them as optional parameters, and in case
# they're used it will save them as the new default credentials. 
# Emits [gj_auth_completed] signal if completed successfully or [gj_auth_failed] if not.
func self_login(p_username: String = _username, p_user_token: String = _user_token) -> void:
	if _username != p_username:
		_username = p_username
	if _user_token != p_user_token:
		_user_token = p_user_token
	
	if has_player_credentials():
		var request = eh_GJUsersAuthRequest.new()
		add_child(request, true)
		request.connect("gj_auth_data_received", self, "_on_self_login_data_received")
		request.connect("gj_request_failed", self, "_on_self_login_request_failed")
		
		var error: int = request.auth_user_credentials(p_username, p_user_token)
		if error != OK:
			_send_request_error(error)


# Sends auth request for the username and token passsed. Will not overwrite default credentials.
# Emits [gj_auth_completed] signal if completed successfully or [gj_auth_failed] if not.
func request(p_username: String, p_user_token: String) -> void:
	var request = eh_GJUsersAuthRequest.new()
	add_child(request, true)
	request.connect("gj_auth_data_received", self, "_on_gj_auth_data_received")
	request.connect("gj_request_failed", self, "_on_gj_request_failed")
	
	var error: int = request.auth_user_credentials(p_username, p_user_token)
	if error != OK:
		_send_request_error(error)
	
### ---------------------------------------


### Private Methods -----------------------
func _send_request_error(error: int) -> void:
	var error_msg = "Request failed with Error Code: %s"%[error]
	error_msg += "\nCheck @GlobalScope in the documentation for a bit more info."
	emit_signal("gj_auth_failed", {"request_error": error_msg})


func _auto_set_user_credentials() -> void :
	# This only works if the player downloaded the game through gamejolt's client. 
	# Otherwise they'll have to manually login.
	var credentials_path = ".gj-credentials"
	var credentials_file = File.new()
	
	if ResourceLoader.exists(USER_CREDENTIALS_PATH):
		var user_credentials: eh_GJUserCredentials = ResourceLoader.load(USER_CREDENTIALS_PATH)
		_username = user_credentials.username
		_user_token = user_credentials.user_game_token
	elif credentials_file.file_exists(credentials_path):
		_set_user_credentials_based_on_gj_file(credentials_file, credentials_path)


func _set_user_credentials_based_on_gj_file(credentials_file: File, credentials_path: String) -> void:
	var success = credentials_file.open(credentials_path,File.READ)
	if success == OK:
		var content = credentials_file.get_as_text()
		var lines = content.split("\n")
		
		_username = lines[1]
		_user_token = lines[2]
		
		_save_user_credentials()


func _save_user_credentials() -> void:
	var user_credentials: eh_GJUserCredentials = eh_GJUserCredentials.new(_username, _user_token)
	ResourceSaver.save(USER_CREDENTIALS_PATH, user_credentials)


func _on_gj_auth_data_received(auth_data: eh_GJUsersAuthData) -> void:
	emit_signal("gj_auth_completed", auth_data)


func _on_gj_request_failed(error_dict: Dictionary) -> void:
	emit_signal("gj_auth_failed", error_dict)


func _on_self_login_data_received(auth_data: eh_GJUsersAuthData) -> void:
	is_logged_in = true
	_save_user_credentials()
	
	emit_signal("gj_auth_completed", auth_data)


func _on_self_login_request_failed(error_dict: Dictionary) -> void:
	is_logged_in = false
	emit_signal("gj_auth_failed", error_dict)

### ---------------------------------------
